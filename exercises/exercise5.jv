pipeline GTFSDataPipeline {

    GTFSZipDownloader 
        -> GTFSZipExtractor
        -> StopsFileSelector
        -> TextFileInterpreterBlock
        -> GTFSCSVParser
        -> StopsTableValidator
        -> StopsDatabaseLoader;

    block GTFSZipDownloader oftype HttpExtractor {
        url: "https://gtfs.rhoenenergie-bus.de/GTFS.zip";
    }

    block GTFSZipExtractor oftype ArchiveInterpreter {
        archiveType: "zip";
    }

    block StopsFileSelector oftype FilePicker {
        path: "/stops.txt";
    }

    block TextFileInterpreterBlock oftype TextFileInterpreter {
    }

    block GTFSCSVParser oftype CSVInterpreter {
        enclosing: '"';
        delimiter: ",";
    }

    valuetype Coordinate oftype decimal {
        constraints: [ GeoCoordinateConstraint ];
    }

    constraint GeoCoordinateConstraint oftype RangeConstraint {
        lowerBound: -90;
        upperBound: 90;
        upperBoundInclusive: true;
    }

    valuetype ZoneFilter oftype integer {
        constraints: [ Zone1645Constraint ];
    }

    constraint Zone1645Constraint oftype RangeConstraint {
        lowerBound: 1645;
        upperBound: 1645;
        upperBoundInclusive: true;
    }

    block StopsTableValidator oftype TableInterpreter {
        header: true;
        columns: [
            "stop_id" oftype integer,
            "stop_name" oftype text,
            "stop_lat" oftype Coordinate,
            "stop_lon" oftype Coordinate,
            "zone_id" oftype ZoneFilter
        ];
    }

    block StopsDatabaseLoader oftype SQLiteLoader {
        table: "stops";
        file: "./gtfs_database.sqlite";
    }

}
